
enum Type_Kind {
    Unresolved,
    Void,
    Any,
    Type,
    Basic,
    Array,
    Struct,
    Union,
    Enum,
    Function,
    Pointer,
};

enum Basic_Type_Info {
    Bool,
    S8,
    S16,
    S32,
    S64,
    U8,
    U16,
    U32,
    U64,
    F32,
    F64,
    String,
    CString
}

struct Type_Info {
    Type_Kind kind;
    
    union {
        Basic_Type_Info Basic;
        Struct_Type_Info Struct;
        Enum_Type_Info Enum;
        Array_Type_Info Array;
    };
}

struct Struct_Field_Info {
    Type_Info* type;
    string ident;
    s64 offset;
}

struct Struct_Type_Info {
    string ident;
    []Struct_Field_Info fields;
}

struct Enum_Type_Info {
    string ident;
    Type_Info* type;
    []string names;
}

struct Array_Type_Info {
    Type_Info* elem_type;
    smm elem_size;
    smm fixed_count; // -1 => if value stores the count
}

inline Type_Info*
type_info(Type type) {
    return (Type_Info*) type;
}

void
print_newline_with_spaces(int count) {
    print("\n");
    for (int i = 0; i < count; i++) {
        print(" ");
    }
}

s64
data_to_s64(Basic_Type_Info info, void* data) {
    switch (info) {
        case Basic_Type_Info.S8: {
            return *((s8*) data);
        }
        
        case Basic_Type_Info.S16: {
            return *((s16*) data);
        }
        
        case Basic_Type_Info.S32: {
            return *((s32*) data);
        }
        
        case Basic_Type_Info.S64: {
            return *((s64*) data);
        }
    }
    
    return 0;
}

u64
data_to_u64(Basic_Type_Info info, void* data) {
    switch (info) {
        case Basic_Type_Info.U8: {
            return *((u8*) data);
        }
        
        case Basic_Type_Info.U16: {
            return *((u16*) data);
        }
        
        case Basic_Type_Info.U32: {
            return *((u32*) data);
        }
        
        case Basic_Type_Info.U64: {
            return *((u64*) data);
        }
    }
    
    return 0;
}


f64
data_to_f64(Basic_Type_Info info, void* data) {
    switch (info) {
        case Basic_Type_Info.F32: {
            return *((f32*) data);
        }
        
        case Basic_Type_Info.F64: {
            return *((f64*) data);
        }
    }
    
    return 0.0f;
}

void
print_data_from_type_info(Type_Info* type, void* data, int indent=0) {
    switch (type.kind) {
        case Type_Kind.Basic: {
            switch (type.Basic) {
                case Basic_Type_Info.Bool: {
                    if (*((bool*) data)) {
                        print("true")
                    } else {
                        print("false")
                    }
                }
                
                case Basic_Type_Info.S8:
                case Basic_Type_Info.S16:
                case Basic_Type_Info.S32:
                case Basic_Type_Info.S64: {
                    print("%", data_to_s64(type.Basic, data));
                }
                
                case Basic_Type_Info.U8:
                case Basic_Type_Info.U16:
                case Basic_Type_Info.U32:
                case Basic_Type_Info.U64: {
                    print("%", data_to_u64(type.Basic, data));
                }
                
                case Basic_Type_Info.F32:
                case Basic_Type_Info.F64: {
                    print("%", data_to_f64(type.Basic, data));
                }
                
                case Basic_Type_Info.String: {
                    print("%", *((string*) data));
                }
                
                case Basic_Type_Info.CString: {
                    print("%", *((cstring*) data));
                }
            }
        }
        
        case Type_Kind.Enum: {
            s64 value = 0;
            switch (type.Enum.type.kind) {
                case Basic_Type_Info.S8:
                case Basic_Type_Info.S16:
                case Basic_Type_Info.S32:
                case Basic_Type_Info.S64: {
                    value = data_to_s64(type.Enum.type.kind, data);
                }
                
                case Basic_Type_Info.U8:
                case Basic_Type_Info.U16:
                case Basic_Type_Info.U32:
                case Basic_Type_Info.U64: {
                    value = data_to_u64(type.Enum.type.kind, data);
                }
            }
            
            print("%.", type.Enum.ident);
            if (value >= 0 && value < type.Enum.names.count) {
                print("% (%)", type.Enum.names[value], value);
            } else {
                print("%", value);
            }
        }
        
        case Type_Kind.Array: {
            print("{ ");
            
            smm count = type.Array.fixed_count;
            if (count < 0) {
                count = *((smm*) data);
            }
            
            Type_Info* elem_type = type.Array.elem_type;
            
            const int max_elements_to_show = 5;
            
            for (int array_index = 0; 
                 array_index < count && array_index < max_elements_to_show; 
                 array_index++) {
                
                if (elem_type.kind != Type_Kind.Basic) {
                    print_newline_with_spaces(indent + 4);
                }
                
                smm offset = type.Array.elem_size*array_index;
                
                print_data_from_type_info(elem_type, (u8*) data + offset, indent + 4);
                if (array_index + 1 < count) {
                    print(", ");
                }
            }
            
            if (count >= max_elements_to_show) {
                if (elem_type.kind != Type_Kind.Basic) {
                    print_newline_with_spaces(indent + 4);
                }
                print("...+% more", count - max_elements_to_show);
            }
            
            if (elem_type.kind != Type_Kind.Basic) {
                print_newline_with_spaces(indent);
                print("}");
            } else {
                print(" }");
            }
        }
        
        case Type_Kind.Union:
        case Type_Kind.Struct: {
            print("% {", type.Struct.ident);
            indent += 4;
            for (int field_index = 0; field_index < type.Struct.fields.count; field_index++) {
                Struct_Field_Info field = type.Struct.fields[field_index];
                if (field.offset == 0 && type.kind == Type_Kind.Union && field_index > 0) {
                    break;
                }
                
                print_newline_with_spaces(indent);
                print("%: ", field.ident);
                print_data_from_type_info(field.type,  (u8*) data + field.offset, indent);
                
                if (field_index + 1 < type.Struct.fields.count) {
                    print(", ");
                }
            }
            indent -= 4;
            print_newline_with_spaces(indent);
            print("}");
        }
    }
    
    if (indent == 0) {
        print("\n");
    }
}

#define print_var(var) \
print_data_from_type_info(type_of(var), &var);